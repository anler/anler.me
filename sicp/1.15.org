---
title: SICP: Exercise 1.15
---

The sine of an angle (specified in radians) can be computed by making use of the approximation =sin x ≈ x= if =x= is sufficiently small, and the trigonometric identity:

\begin{equation}
\sin x = 3\sin \frac{x}{3} - 4\sin^3\frac{x}{3}
\end{equation}

to reduce the size of the argument of =sin=. (For purposes of this exercise an angle is considered "sufficiently small" if its magnitude is not greater that 0.1 radians)
These ideas are incorporated in the following procedures:

#+BEGIN_SRC scheme
  (define (cube x) (* x x x))

  (define (p x) (- (* 3 x)
                   (* 4 (cube x))))

  (define (sine angle)
    (if (not (> (abs angle) 0.1))
        angle
        (p (sine (/ angle 3.0)))))
#+END_SRC

1. How many times is the procedure =p= applied when =(sine 12.15)= is evaluated?
2. What is the order of growth in space and number of steps (as a function of =a=) used by the process generated by the =sine= procedure when =(sine a)= is evaluated?

* Solution
** 1. How many times is the procedure =p= applied when =(sine 12.15)= is evaluated?

Let's use the substitution model to watch the process drawn by the procedure.

#+BEGIN_SRC scheme
  (sine 12.15)

  ;; →

  (if (not (> (abs 12.15)
              0.1))
      angle
      (p (sine (/ 12.15 3.0))))

  ;; →

  (p
   (if (not (> (abs 4.05)
               0.1))
       angle
       (p (sine (/ 4.05 3.0)))))

  ;; →

  (p
   (p
    (if (not (> (abs 1.3499999999999999)
                0.1))
        angle
        (p (sine (/ 1.3499999999999999 3.0))))))

  ;; →

  (p
   (p
    (p
     (if (not (> (abs 0.44999999999999996)
                 0.1))
         angle
         (p (sine (/ 0.44999999999999996 3.0)))))))

  ;; →

  (p
   (p
    (p
     (p
      (if (not (> (abs 0.15)
                  0.1))
          angle
          (p (sine (/ 0.15 3.0))))))))

  ;; →

  (p
   (p
    (p
     (p
      (p
       (if (not (> (abs 0.049999999999999996)
                   0.1))
           angle
           (p (sine (/ 0.049999999999999996 3.0)))))))))

  ;; →

  (p
   (p
    (p
     (p
      (p
       0.049999999999999996)))))
#+END_SRC

So the answer is *5 times*.

We can easily test the answer with the following Haskell script (save it and run it with =runhaskell sine.hs=).

#+BEGIN_SRC haskell
  import           Control.Monad.Trans.Writer
  import           Data.Monoid

  main = display . getSum . snd . runWriter $ sine 12.15
    where display t = putStrLn $ "p was called "++show t++" times."

  sine angle | abs angle < 0.1 = writer (angle, Sum 0)
             | otherwise = p =<< (sine (angle / 3.0))

  p x = writer (3*x - 4*x^3, 1)
#+END_SRC

When I run it the output is *p was called 5 times.*

** 2. What is the order of growth in space and number of steps (as a function of =a=) used by the process generated by the =sine= procedure when =(sine a)= is evaluated?

If we pay attention to the substitution we did before we can easily spot  that the generated process is recursive. In each step, the problem size =a= is reduced in =2/3=, that is, the next step receives only =1/3= of the original size of the problem meaning that its growth in space and time must be a function of =Θ(log a)=.

In the first question, we could have used this knowledge to easily find the answer, that is, we were looking for the solution of the equation:

\begin{equation}
\frac{12.15}{3^n} < 0.1
\end{equation}

which is =n ≥ 5=, meaning is called 5 times in the case of our procedure.
